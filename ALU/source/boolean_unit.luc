module boolean_unit (
    input a[16],
    input b[16],
    input alufn[6],
    output out[16]
  ) {

  sig inputs[2], func[4];
  var i;
  
  always {
    out = 0;
    
    // We use the lower 4 bits of alufn as a lookup table
    // for the output of the binary function. We select
    // from it with a and b, which we can think of as
    // either separate inputs or combined as a two-bit index
    // from 0 to 3. For example, with alufn[3:0] being 1000,
    // we get:
    //
    // b | a | func
    // 1 | 1 | 1
    // 1 | 0 | 0
    // 0 | 1 | 0
    // 0 | 0 | 0
    //
    // Which is an AND gate.
    
    /*
    BUGFIX: swapped a and b (b is msb in lab3 spec)
    "A" operation in ALU is 0x1A = 26 = 6b011010
    which has a func of 1010
    https://natalieagus.github.io/50002/assets/contentimage/lab3/9.png
    -----------------------------
    b | a | func "A"
    1 | 1 | 1
    1 | 0 | 0
    0 | 1 | 1
    0 | 0 | 0
    */
    func = alufn[3:0];
    
    // We do this for all the bits in out
    for (i=0; i<16; i++) {
      inputs[1] = a[i];
      inputs[0] = b[i];
      out[i] = func[inputs];
    }
  }
}
